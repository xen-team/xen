layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(r16f, binding = 0) uniform writeonly restrict image2D uniNoiseMap;
uniform float uniNoiseFactor = 0.01;

const vec2 offsets[9] = vec2[](
  vec2(-1.0,  1.0), vec2(0.0,  1.0), vec2(1.0,  1.0),
  vec2(-1.0,  0.0), vec2(0.0,  0.0), vec2(1.0,  0.0),
  vec2(-1.0, -1.0), vec2(0.0, -1.0), vec2(1.0, -1.0)
);

vec2 hash(vec2 vec) {
  // "Hash without Sine", from https://www.shadertoy.com/view/4djSRW
  vec3 v3 = fract(vec3(vec.xyx) * vec3(0.1031, 0.1030, 0.0973));
  v3     += dot(v3, v3.yzx + 33.33);
  return fract((v3.xx + v3.yz) * v3.zy);
}

float computeWorley(vec2 coords) {
  vec2 intCoords   = floor(coords);
  vec2 fractCoords = coords - intCoords; // fract(coords)

  float minSqDist = 1.0;

  for (int i = 0; i < 9; ++i) {
    vec2 cellCoords   = hash(intCoords + offsets[i]);
    vec2 cellPoint    = cellCoords + offsets[i] - fractCoords;
    float pointSqDist = dot(cellPoint, cellPoint);

    minSqDist = min(minSqDist, pointSqDist);
  }

  return sqrt(minSqDist);
}

void main() {
  float noise = computeWorley(vec2(gl_GlobalInvocationID.xy) * uniNoiseFactor);

  ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
  imageStore(uniNoiseMap, pixelCoords, vec4(vec3(noise), 1.0));
}
